plugins {
    id 'java'
    id 'war'
    id 'eclipse'
    id 'idea'
    id 'org.akhikhl.gretty' version '3.0.6'  // specify fixed gretty plugin version here
}

version = "1.1"

defaultTasks 'clean', 'build'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    testImplementation 'org.testng:testng:6.9.13.6'
}

gretty {
    servletContainer = 'jetty9'
    httpPort = 8080
    contextPath = '/gradle-war'
}

war {
    archiveFileName = 'gradle-war.war'
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    useTestNG {
        excludeGroups 'integration'
    }
}

tasks.register('integrationTest', Test) {
    dependsOn test
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    useTestNG {
        includeGroups 'integration'
    }
}
